% file: appendix-ajupiter-impl-xjupiter-tla.tex

\documentclass{article}
\usepackage{graphicx}
\usepackage{tlatex}
\usepackage{color}
\definecolor{boxshade}{gray}{.85}
\setboolean{shading}{true}

\begin{document}
\begin{tla}
------------------------ MODULE AJupiterImplXJupiter ------------------------
EXTENDS AJupiterExtended \* $AJupiter$ extended with $JupiterCtx$.
-----------------------------------------------------------------------------
VARIABLES c2ss, s2ss

varsImpl == <<varsEx, c2ss, s2ss>>
-----------------------------------------------------------------------------
InitImpl ==
    /\ InitEx
    /\ c2ss = [c \in Client |-> EmptySS]    
    /\ s2ss = [c \in Client |-> EmptySS]    

DoOpImpl(c, op) == 
    /\ DoOpEx(c, op)
    /\ LET cop == [op |-> op, oid |-> [c |-> c, seq |-> cseq[c]], ctx |-> ds[c]] 
       IN  c2ss' = [c2ss EXCEPT ![c] = 
                        @ (+) [node |-> {ds'[c]},
                               edge |-> {[from |-> ds[c], to |-> ds'[c], cop |-> cop]}]]
    /\ UNCHANGED s2ss

ClientPerformImpl(c, m) ==
    /\ LET xform == xFormCopCopsShift(m.cop, cbuf[c], m.ack) \* $[xcop, xss, lss]$
       IN  c2ss' = [c2ss EXCEPT ![c] = @ (+) xform.xss]
    /\ UNCHANGED s2ss

ServerPerformImpl(m) ==
    /\ LET c == ClientOf(m.cop)
           xform == xFormCopCopsShift(m.cop, sbuf[c], m.ack) \* $[xcop, xss, lss]$
       IN  s2ss' = [cl \in Client |-> IF cl = c THEN s2ss[cl] (+) xform.xss 
                                                ELSE s2ss[cl] (+) xform.lss]
    /\ UNCHANGED c2ss
-----------------------------------------------------------------------------
DoImpl(c) == 
    /\ DoCtx(c)
    /\ DoInt(DoOpImpl, c)
    /\ UNCHANGED <<sbuf, srec>>

RevImpl(c) ==
    /\ RevEx(c)
    /\ RevInt(ClientPerformImpl, c)

SRevImpl ==
    /\ SRevEx
    /\ SRevInt(ServerPerformImpl)
-----------------------------------------------------------------------------
NextImpl ==
    \/ \E c \in Client: DoImpl(c) \/ RevImpl(c)
    \/ SRevImpl

SpecImpl == InitImpl /\ [][NextImpl]_varsImpl
-----------------------------------------------------------------------------
XJ == INSTANCE XJupiter WITH Msg <- Cop,
         cincoming <- cincomingXJ, sincoming <- sincomingXJ,
         c2ss <- c2ss, s2ss <- s2ss

THEOREM SpecImpl => XJ!Spec
=============================================================================
\end{tla}
\end{document}
