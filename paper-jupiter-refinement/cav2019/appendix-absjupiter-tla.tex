% file: appendix-absjupiter-tla.tex

\documentclass{article}
\usepackage{graphicx}
\usepackage{tlatex}
\usepackage{color}
\definecolor{boxshade}{gray}{.85}
\setboolean{shading}{true}

\begin{document}
\begin{tla}
----------------------------- MODULE AbsJupiter -----------------------------
(*
Abstract $Jupiter$, inspired by the COT algorithm~\cite{Sun:TPDS09}.
*)
----------------------------------------------------------------------------
VARIABLES copss \* $copss[r]$: the set of context-based operations maintained at replia $r$

vars == <<intVars, ctxVars, serialVars, copss>>
----------------------------------------------------------------------------
NextCop(r, cop, ss, ctx) == \* The next $fcop$ against which $cop$ is to be transformed.
    LET foid == CHOOSE oid \in ctx: \* the first $oid$ in $ctx$ according to $serial[r]$
                    \A id \in ctx \ {oid}: tb(oid, id, serial[r])
    IN  CHOOSE fcop \in ss: fcop.oid = foid /\ fcop.ctx = cop.ctx 

Perform(r, cop) ==
    LET xform == xForm(NextCop, r, cop, copss[r])
    IN  /\ copss' = [copss EXCEPT ![r] = xform.xss]
        /\ SetNewAop(r, xform.xcop.op)
        
ClientPerform(c, cop) == Perform(c, cop)
        
ServerPerform(cop) == 
    /\ Perform(Server, cop)
    /\ Comm!SSendSame(ClientOf(cop), cop) \* broadcast $cop$ it receives

DoOp(c, op) ==
    LET cop == [op |-> op, oid |-> [c |-> c, seq |-> cseq[c]], ctx |-> ds[c]]
    IN  /\ ClientPerform(c, cop)
        /\ Comm!CSend(cop) 
----------------------------------------------------------------------------
Init ==
    /\ InitInt
    /\ InitCtx
    /\ InitSerial
    /\ copss = [r \in Replica |-> {}]

Do(c) == 
    /\ DoInt(DoOp, c)
    /\ DoCtx(c)
    /\ DoSerial(c)

Rev(c) ==
    /\ RevInt(ClientPerform, c)
    /\ RevCtx(c)
    /\ RevSerial(c)

SRev ==
    /\ SRevInt(ServerPerform)
    /\ SRevCtx
    /\ SRevSerial
-----------------------------------------------------------------------------
Next ==
    \/ \E c \in Client: Do(c) \/ Rev(c)
    \/ SRev

Spec == Init /\ [][Next]_vars
=============================================================================
\end{tla}
\end{document}
