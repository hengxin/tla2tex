% file: appendix-xjupiter-tla.tex

\documentclass{article}
\usepackage{graphicx}
\usepackage{tlatex}
\usepackage{color}
\definecolor{boxshade}{gray}{.85}
\setboolean{shading}{true}

\begin{document}
\begin{tla}
------------------------------ MODULE XJupiter ------------------------------
(*
Specification of the $Jupiter$ protocol described in $CSCW'2014$ by Xu, Sun, and Li~\cite{Xu:CSCW14}.
*)
-----------------------------------------------------------------------------
VARIABLES c2ss, \* $c2ss[c]$: the $2D$ state space ($2ss$, for short) at client $c$ 
          s2ss  \* $s2ss[c]$: the $2D$ state space maintained by the $Server$ for client $c$

vars == <<intVars, ctxVars, c2ss, s2ss>>
-----------------------------------------------------------------------------
NextEdge(r, u, ss) == \* Return the unique outgoing edge from $u$ in $2D$ state space $ss$ 
    CHOOSE e \in ss.edge: e.from = u \* before a transformation at $u$ ($r$ is not used). 

ClientPerform(c, cop) == 
    LET xform == xForm(NextEdge, c, cop, c2ss[c])
    IN  /\ c2ss' = [c2ss EXCEPT ![c] = @ (+) xform.xss]
        /\ SetNewAop(c, xform.xcop.op)

ServerPerform(cop) == 
    LET c == ClientOf(cop)
    xform == xForm(NextEdge, Server, cop, s2ss[c]) \* $xform$: $[xcop, xss, lss]$
     xcop == xform.xcop
    IN  /\ s2ss' = [cl \in Client |-> IF cl = c THEN s2ss[cl] (+) xform.xss 
                                                ELSE s2ss[cl] (+) xform.lss]
        /\ SetNewAop(Server, xcop.op)
        /\ Comm!SSendSame(c, xcop)  \* broadcast the transformed $xcop$ 

DoOp(c, op) == 
    LET cop == [op |-> op, oid |-> [c |-> c, seq |-> cseq[c]], ctx |-> ds[c]] 
    IN  /\ ClientPerform(c, cop)
        /\ Comm!CSend(cop)
-----------------------------------------------------------------------------
Init == 
    /\ InitInt
    /\ InitCtx
    /\ c2ss = [c \in Client |-> EmptySS]
    /\ s2ss = [c \in Client |-> EmptySS]

Do(c) == 
    /\ DoInt(DoOp, c)
    /\ DoCtx(c)
    /\ UNCHANGED s2ss

Rev(c) == 
    /\ RevInt(ClientPerform, c)
    /\ RevCtx(c)
    /\ UNCHANGED s2ss

SRev == 
    /\ SRevInt(ServerPerform)
    /\ SRevCtx
    /\ UNCHANGED c2ss
-----------------------------------------------------------------------------
Next == 
    \/ \E c \in Client: Do(c) \/ Rev(c)
    \/ SRev

Spec == Init /\ [][Next]_vars
=============================================================================
\end{tla}
\end{document}
